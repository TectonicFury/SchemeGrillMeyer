#!/usr/local/bin/guile -s
!#

(define (eval exp env)

  (cond ((self-evaluating? exp) exp) ; for self-evaluating expressions, such as numbers, eval returns the expression itself

        ((variable? exp) (lookup-variable-value exp env)); eval must look up variables in the environment to find their values

        ((quoted? exp) (text-of-quotation exp)); for quoted expressions, eval returns the expression that was quoted

        ((assignment? exp) (eval-assignment exp env)); an assignment to (or definition of) a variable must recursively call eval to --
                                                     ;-- compute the new value to be associated with the variable.The environment --
                                                     ;-- must be modified to change (or create) the binding of the variable

        ((definition? exp) (eval-definition exp env))

        ((if? exp) (eval-if exp env)); an if exp requires special processing of its parts, so as to evaluate the consequent
                                     ; if the predicate is true, and otherwise to evaluate the alternative

        ((while? exp) (eval-while exp env)) ; ex-4.9

        ((for? exp) (eval (for->combination exp) env)) ; ex-4.9

        ((let? exp) (eval (let->combination exp) env)) ; ex-4.6

        ((letrec? exp) (eval (letrec->let exp) env)) ; ex 4.20

        ((lambda? exp); a lambda expression must be transformed into an applicable procedure by packaging together the parameters --
                      ; -- and body specified by the lambda expression with the environment of the evaluation
          (make-procedure (lambda-parameters exp)
                          (lambda-body exp)
                          env))
        ((begin? exp); a begin expression requires evaluating its sequence of expressions in the order in which they appear
          (eval-sequence (begin-actions exp) env))
        ((cond? exp) (eval (cond->if exp) env)); a case analysis (cond) is transformed into a nest of if expressions and then --
                                               ; -- evaluated
        ((application? exp); for a procedure application, eval musty recursively evaluate the operator part and the operands of the --
                           ; -- combination. The resulting procedure and arguments are passed to apply, which handles the actual --
                           ; procedure application.
          (apply (eval (operator exp) env) (list-of-values (operands exp) env)))
        (else (error "Unknown expression type -- EVAL" exp))))


(define apply-in-underlying-scheme apply)

(define (apply procedure arguments)
  (cond ((primitive-procedure? procedure)
          (apply-primitive-procedure procedure arguments))
        ((compound-procedure? procedure)
          (eval-sequence
            (procedure-body procedure)
            (extend-environment
              (procedure-parameters procedure)
              arguments
              (procedure-environment procedure))))
        (else (error "Unknown procedure type: APPLY" procedure))))

(define (list-of-values exps env)
  (if (no-operands? exps)
      '()
      (cons (eval (first-operand exps) env) (list-of-values (rest-operands exps) env))))

(define (eval-if exp env)
  (if (true? (eval (if-predicate exp) env))
      (eval (if-consequent exp) env)
      (eval (if-alternative exp) env)))
(define (eval-sequence exps env)
  (cond ((last-exp? exps) (eval (first-exp exps) env))
        (else (eval (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))

(define (eval-assignment exp env)
  (set-variable-value! (assignment-variable exp) (eval (assignment-value exp) env) env)
  'ok)
(define (eval-definition exp env)
  (define-variable! (definition-variable exp) (eval (definition-value exp) env) env)
  'ok)

;section 4.1.2

(define (self-evaluating? exp)
  (cond ((number? exp) #t)
        ((string? exp) #t)
        (else #f)))

(define (variable? exp) (symbol? exp))

(define (quoted? exp)
  (tagged-list? exp 'quote))

(define (text-of-quotation exp) (cadr exp))

(define (tagged-list? exp tag)
  (if (pair? exp)
      (eq? (car exp) tag)
      #f))

(define (assignment? exp)
  (tagged-list? exp 'set!))
(define (assignment-variable exp)
  (cadr exp))
(define (assignment-value exp) (caddr exp))

(define (definition? exp)
  (tagged-list? exp 'define))
(define (definition-variable exp)
  (if (symbol? (cadr exp))
      (cadr exp)
      (caadr exp)))
(define (definition-value exp)
  (if (symbol? (cadr exp))
      (caddr exp)
      (make-lambda (cdadr exp) (cddr exp))))

(define (scan-out-defines body) ; ex-4.16
  (define (iter-def bdy);take out defines
    (if (null? bdy)
        '()
        (let ((exp (car bdy)))
          (if (definition? exp)
              (cons (list (definition-variable exp) ''*unassigned*) (iter-def (cdr bdy)))
              (iter-def (cdr bdy))))))
  (define (iter-non-def bdy);take out non defines
    (if (null? bdy)
        '()
        (let ((exp (car bdy)))
          (if (not (definition? exp))
              (cons exp (iter-non-def (cdr bdy)))
              (iter-non-def (cdr bdy))))))
  (define (iter-set-list bdy) ; the set! statements
    (if (null? bdy)
        '()
        (let ((exp (car bdy)))
          (if (definition? exp)
              (cons (list 'set! (definition-variable exp) (definition-value exp))
                    (iter-set-list (cdr bdy)))
              (iter-set-list (cdr bdy))))))

  (let ((iter-val (iter-def body))
        (iter-non-val (iter-non-def body))
        (iter-set (iter-set-list body)))
    (if (null? iter-val)
        body
        (list (append (list 'let iter-val) iter-set iter-non-val)))))

(define (lambda? exp) (tagged-list? exp 'lambda))
(define (lambda-parameters exp) (cadr exp))
(define (lambda-body exp) (cddr exp))
(define (make-lambda parameters body)
  (cons 'lambda (cons parameters body)))

(define (if? exp) (tagged-list? exp 'if))
(define (if-predicate exp) (cadr exp))
(define (if-consequent exp) (caddr exp))
(define (if-alternative exp)
  (if (not (null? (cdddr exp)))
      (cadddr exp)
      'false))
(define (make-if predicate consequent alternative)
  (list 'if predicate consequent alternative))

(define (begin? exp)
  (tagged-list? exp 'begin))
(define (begin-actions exp) (cdr exp))
(define (last-exp? seq) (null? (cdr seq)))
(define (first-exp seq) (car seq))
(define (rest-exps seq) (cdr seq))

(define (sequence->exp seq)
  (cond ((null? seq) seq)
        ((last-exp? seq) (first-exp seq))
        (else (make-begin seq))))
(define (make-begin seq) (cons 'begin seq))

(define (application? exp) (pair? exp))
(define (operator exp) (car exp))
(define (operands exp) (cdr exp))
(define (no-operands? ops) (null? ops))
(define (first-operand ops) (car ops))
(define (rest-operands ops) (cdr ops))

;derived expressions
(define (cond? exp) (tagged-list? exp 'cond))
(define (cond-clauses exp) (cdr exp))
(define (cond-else-clause? clause)
  (eq? (cond-predicate clause) 'else))
(define (cond-predicate clause) (car clause))
(define (cond-actions clause) (cdr clause))
(define (cond->if exp)
  (expand-clauses (cond-clauses exp)))
(define (expand-clauses clauses)
  (if (null? clauses)
      'false
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (sequence->exp (cond-actions first))
                (error "ELSE clause isn't last -- COND->IF" clauses))
            (make-if (cond-predicate first)
                     (sequence->exp (cond-actions first))
                     (expand-clauses rest))))))


;section 4.1.3
(define (true? x) (not (eq? x #f)))
(define (false? x) (eq? x #f))
(define (make-procedure parameters body env)
;(list 'procedure parameters body env))
  (list 'procedure parameters (scan-out-defines body) env))
(define (compound-procedure? p)
  (tagged-list? p 'procedure))
(define (procedure-parameters p)
  (cadr p))
(define (procedure-body p) (caddr p))
(define (procedure-environment p) (cadddr p))
(define (enclosing-environment env) (cdr env))
(define (first-frame env) (car env))
(define the-empty-environment '())

(define (make-frame variables values)
  (cons variables values))
(define (frame-variables frame) (car frame))
(define (frame-values frame) (cdr frame))
(define (add-binding-to-frame! var val frame)
  (set-car! frame (cons var (car frame)))
  (set-cdr! frame (cons val (cdr frame))))

(define (extend-environment vars vals base-env)
  (if (= (length vars) (length vals))
      (cons (make-frame vars vals) base-env)
      (if (< (length vars) (length vals))
          (error "Too many arguments supplied" vars vals)
          (error "Too few arguments supplied" vars vals))))

; (define (lookup-variable-value var env)
;   (define (env-loop env)
;     (define (scan vars vals)
;       (cond ((null? vars) (env-loop (enclosing-environment env)))
;             ((eq? var (car vars)) (car vals))
;             (else (scan (cdr vars) (cdr vals)))))
;     (if (eq? env the-empty-environment)
;         (error "Unbound variable" var)
;         ;#f
;         (let ((frame (first-frame env)))
;           (scan (frame-variables frame)
;                 (frame-values frame)))))
;   (env-loop env))

(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars) (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
              (if (eq? (car vals) '*unassigned*)
                  (error "Attempt to use unassigned variable -- LOOKUP" var)
                  (car vals)))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        ;#f
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))

(define (set-variable-value! var val env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars) (env-loop (enclosing-environment env)))
            ((eq? var (car vars)) (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable -- SET!" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame) (frame-values frame)))))
  (env-loop env))

(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (define (scan vars vals)
      (cond ((null? vars) (add-binding-to-frame! var val frame))
            ((eq? var (car vars)) (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (scan (frame-variables frame) (frame-values frame))))
;;solutions to exercises

; section 4.1.4

(define (setup-environment)
  (let ((initial-env
          (extend-environment
            (primitive-procedure-names) (primitive-procedure-objects) the-empty-environment)))
    (define-variable! 'true #t initial-env)
    (define-variable! 'false #f initial-env)
  initial-env))

(define (primitive-procedure? proc) (tagged-list? proc 'primitive))
(define (primitive-implementation proc) (cadr proc))

(define (sqrt-newton n)
  (define (iter x)
    (if (< (abs (- (* x x) n)) 0.00000001)
        x
        (iter (- x (/ (- (* x x) n) (* 2 x))))))
  (iter 1.0))
;

(define (print val)
  (display val)
  (newline))
(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
          (if (same-variable? exp var) 1 0))
        ((sum? exp) (make-sum (deriv (addend exp) var)
                              (deriv (augend exp) var)))
        ((product? exp) (make-sum
                              (make-product (multiplier exp) (deriv (multiplicand exp) var))
                              (make-product (deriv (multiplier exp) var) (multiplicand exp))))
        ((exponentiation? exp)
          (make-product (exponent exp)
                        (make-product (make-exponent (base exp) (- (exponent exp) 1)) (deriv (base exp) var))))
        (else
          (error "unknown expression type: DERIV" exp))))

(define primitive-procedures
  (list (list 'list list)
        (list 'car car)
        (list 'cdr cdr)
        (list 'cons cons)
        (list 'null? null?)
        (list '+ +)
        (list '- -)
        (list '* *)
        (list '/ /)
        (list '< <)
        (list '> >)
        (list '= =)
        (list 'abs abs)
        (list 'sqrt sqrt)
        (list 'log log)
        (list 'deriv deriv)
        (list 'print print)
        (list 'display display)))

(define (primitive-procedure-names) (map car primitive-procedures))

(define (primitive-procedure-objects)
  (map (lambda (proc) (list 'primitive (cadr proc))) primitive-procedures))

(define (apply-primitive-procedure proc args)
  (apply-in-underlying-scheme (primitive-implementation proc) args))

(define input-prompt "M-Eval input:")
(define output-prompt "M-Eval value:")

(define (driver-loop)
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (if (eof-object? input)
        (begin (display "Ma'a Salama") (newline)(exit))
        (let ((output (eval input the-global-environment)))
             (announce-output output-prompt)
             (user-print output))))
(driver-loop))

(define (prompt-for-input string)
(newline)(newline)(display string) (newline))

(define (announce-output string) (newline) (display string) (newline))

(define (user-print object)
  (if (compound-procedure? object)
    (display (list 'compound-procedure (procedure-parameters object) (procedure-body object) '<procedure-env>)) (display object)))


;; additional forms

;(for (initialise-bindings) (condition-to-check-evry-loop) (update-counter) body)
(define (for? exp) (tagged-list? exp 'for))
(define (for-initial-var-binds exp) (cadr exp))
(define (for-counter exp) (car (cadr exp)))
(define (for-counter-val exp) (cadr (cadr exp)))
(define (for-predicate exp) (caddr exp))
(define (for-update exp) (cadddr exp))
(define (for-body exp) (cddddr exp))

(define (for->combination exp)
  (list 'let '()
    (list 'define
      (list 'proc (for-counter exp))
        (list 'if (for-predicate exp)
                  (make-begin (list (make-begin (for-body exp)) (for-update exp) (list 'proc (for-counter exp))))
                  ''for-done))
    (list 'proc (for-counter-val exp)) ; calling the proc function
    ))

(define (while? exp)
  (tagged-list? exp 'while))
; (while predicate-is-true body) -- this is the form

(define (while-predicate exp) (cadr exp))
(define (while-body exp) (cddr exp))
(define (eval-while exp env)
  (if (true? (eval (while-predicate exp) env))
      (begin (eval-sequence (while-body exp) env)
             (eval-while exp env))
      'while-done))

(define (let? exp) (tagged-list? exp 'let))
(define (let-var-binds exp)
  (cadr exp))
(define (let-variables exp)
  (let ((lvb (let-var-binds exp)))
    (define (iter res rem)
      (if (null? rem)
          res
          (iter (append res (list (caar rem))) (cdr rem))))
  (iter '() lvb)))

(define (let-bindings exp)
  (let ((lvb (let-var-binds exp)))
    (define (iter res rem)
      (if (null? rem)
          res
          (iter (append res (list (cadr (car rem)))) (cdr rem))))
  (iter '() lvb)))

(define (let-body exp)
(cddr exp)) ;<<<<-- lambda-body is (cddr exp), and make-lambda takes (cddr exp) for body straight away, so this is the only way

  ;named-let procedures
(define (named-let? exp)
  (symbol? (cadr exp)))
(define (named-let-def-var exp) ;the name of the would-be procedure
  (cadr exp))
(define (named-let-parameters exp)
  (define (iter res rem)
    (if (null? rem)
        res
        (iter (append res (list(caar rem))) (cdr rem))))
  (iter '() (caddr exp)))

(define (named-let-parameter-bindings exp)
  (define (iter res rem)
    (if (null? rem)
        res
        (iter (append res (list (cadr (car rem)))) (cdr rem))))
  (iter '() (caddr exp)))

(define (named-let-body exp)
  (sequence->exp (cdddr exp)))

(define (let->combination exp)
  (if (named-let? exp)
      (sequence->exp
        (list
            (list 'define
                  (append (list (named-let-def-var exp)) (named-let-parameters exp))
                  (named-let-body exp))
              (append (list (named-let-def-var exp)) (named-let-parameter-bindings exp))))
        (let ((lv (let-variables exp))
             (lb (let-bindings exp))
             (lbody (let-body exp)))
             (cons (make-lambda lv lbody) lb))))

;ex 4.20

(define (letrec? exp) (tagged-list? exp 'letrec))
(define (letrec-inits exp) (cadr exp))
(define (letrec-vars exp) (map car (letrec-inits exp)))
(define (letrec-vals exp) (map cadr (letrec-inits exp)))
(define (letrec-body exp)
  (cddr exp))
(define (letrec->let exp)
  (let ((inits-unassigned (map (lambda (x) (list x ''*unassigned*)) (letrec-vars exp))))
       (define (set-block vars vals)
         (if (null? vars)
             '()
             (cons (list 'set! (car vars) (car vals)) (set-block (cdr vars) (cdr vals)))))
    (append (list 'let) (list inits-unassigned) (set-block (letrec-vars exp) (letrec-vals exp)) (letrec-body exp))))

; sqrt from newton method

  (define the-global-environment (setup-environment))
  (define (same-variable? v1 v2)
    (and (variable? v1) (variable? v2) (eq? v1 v2)))

  (define (=number? exp num)
    (and (number? exp) (= exp num)))

  (define (make-sum a1 a2)
    (cond ((null? a2) a1)
          ((and (pair? a2) (or (sum? a2) (product? a2) (exponentiation? a2)))
            (cond ((=number? a1 0) a2)
                  ((sum? a2) (append (list '+ a1) (cdr a2)));just makes it look more simplified
                  (else (list '+ a1 a2))))
          ((pair? a2); if a2 is simply the list of addend terms, then doing (car a2) won't fetch the '+' symbol when doing make-sum
            (let ((a2sum (make-sum (car a2) (cdr a2))))
              (cond ((=number? a1 0) a2sum)
                    ((=number? a2sum 0) a1)
                    ((and (number? a1) (number? a2sum)) (+ a1 a2sum))
                    (else (list '+ a1 a2sum)))))
            (else
              (cond ((=number? a1 0) a2)
                    ((=number? a2 0) a1)
                    ((and (number? a1) (number? a2)) (+ a1 a2))
                    (else (list '+ a1 a2))))))

  (define (make-product m1 m2)
    (cond ((null? m2) m1)
          ((and (pair? m2) (or (product? m2) (sum? m2) (exponentiation? m2))) ;m2 is having (* ..) form alreadyØŒ so we just make a product out of it
            (cond ((=number? m1 0) 0)
                  ((=number? m1 1) m2)
                  ((product? m2) (append (list '* m1) (cdr m2)))
                  (else (list '* m1 m2))))
          ((pair? m2) ;m2 is just the remainder of the list of multiplcative arguments
            (let ((m2prod (make-product (car m2) (cdr m2))))
              (cond ((or (=number? m1 0) (=number? m2prod 0)) 0)
                    ((=number? m1 1) m2prod)
                    ((=number? m2prod 1) m1)
                    ((and (number? m1) (number? m2prod)) (* m1 m2prod))
                    (else (list '* m1 m2prod)))
              ))
            (else
              (cond ((or (=number? m1 0) (=number? m2 0)) 0)
                    ((=number? m1 1) m2)
                    ((=number? m2 1) m1)
                    ((and (number? m1) (number? m2)) (* m1 m2))
                    (else (list '* m1 m2))))))

  (define (make-exponent u n)
    (cond ((=number? n 0) 1)
          ((=number? n 1) u)
          (else (list '** u n))))

  (define (exponentiation? x)
    (and (pair? x) (eq? (car x) '**)))

  (define (base x)
    (cadr x))

  (define (exponent x)
    (caddr x))

  (define (sum? x) (and (pair? x) (eq? (car x) '+)))

  (define (addend s) (cadr s)) ; if s is a sum it has to be of the form (+ a1 a2 ..), this is for ex-2.57

  (define (augend s)
    (make-sum (caddr s) (cdr (cdr (cdr s)))))

  (define (product? x) (and (pair? x) (eq? (car x) '*)))

  (define (multiplier p) (cadr p))

  (define (multiplicand p)
    (make-product (caddr p) (cdr (cdr (cdr p)))))

  (driver-loop)
